# 内存管理 之 自动释放池  

有如下的示例代码，使用`@autoreleasepool`创建自动释放池。

```objc
int main(int argc, const char * argv[]) {
    @autoreleasepool {
        NSLog(@"Hello World");
    }
    return 0;
}
```
使用`clang -arch x86_64 -rewrite-objc`命令将如上的代码改写为C语言。

```c
int main(int argc, const char * argv[]) {
    /* @autoreleasepool */ { 
    
    __AtAutoreleasePool __autoreleasepool; 
    NSLog((NSString *)&__NSConstantStringImpl__var_folders_34_yg5lfpys2l1ghsm4rjr974qh0000gn_T_main_a28f55_mii_0);

    }
    return 0;
}

```

我们可以看到`@autoreleasepool`变成了`__AtAutoreleasePool`

```c
 /* @autoreleasepool */ { 
    __AtAutoreleasePool __autoreleasepool;
 }

 struct __AtAutoreleasePool {
  __AtAutoreleasePool() {atautoreleasepoolobj = objc_autoreleasePoolPush();}
  ~__AtAutoreleasePool() {objc_autoreleasePoolPop(atautoreleasepoolobj);}
  void * atautoreleasepoolobj;
};
```

## 1. `objc_autoreleasePoolPush`  和 `objc_autoreleasePoolPop`

```c
// objc4.818.2 

void *
objc_autoreleasePoolPush(void)
{
    return AutoreleasePoolPage::push();
}

void
objc_autoreleasePoolPop(void *ctxt)
{
    AutoreleasePoolPage::pop(ctxt);
}
```

在**libObjc**的实现中，`objc_autoreleasePoolPush`和`objc_autoreleasePoolPop` 都是调用 `AutoreleasePoolPage` 类的方法。

## 2. `AutoreleasePoolPage`

```c++

#define PAGE_MIN_SHIFT          12
#define PAGE_MIN_SIZE           (1 << PAGE_MIN_SHIFT)

class AutoreleasePoolPage : private AutoreleasePoolPageData
{
    // 2的12次方 4096字节
    	static size_t const SIZE =
#if PROTECT_AUTORELEASEPOOL
		PAGE_MAX_SIZE;  // must be multiple of vm page size
#else
		PAGE_MIN_SIZE;  // size and alignment, power of 2
#endif

    AutoreleasePoolPage(AutoreleasePoolPage *newParent) :
		AutoreleasePoolPageData(begin(),
								objc_thread_self(),
								newParent,
								newParent ? 1+newParent->depth : 0,
								newParent ? newParent->hiwat : 0)
    {
        if (objc::PageCountWarning != -1) {
            checkTooMuchAutorelease();
        }

        if (parent) {
            parent->check();
            ASSERT(!parent->child);
            parent->unprotect();
            parent->child = this;
            parent->protect();
        }
        protect();
    }

    ~AutoreleasePoolPage() 
    {
        check();
        unprotect();
        ASSERT(empty());

        // Not recursive: we don't want to blow out the stack 
        // if a thread accumulates a stupendous amount of garbage
        ASSERT(!child);
    }

    // 重写了new运算符，申请4096字节的内存，即一个内存页
    static void * operator new(size_t size) {
        return malloc_zone_memalign(malloc_default_zone(), SIZE, SIZE);
    }
    static void operator delete(void * p) {
        return free(p);
    }
};


struct AutoreleasePoolPageData
{
	magic_t const magic;
	__unsafe_unretained id *next;
	pthread_t const thread;
	AutoreleasePoolPage * const parent;
	AutoreleasePoolPage *child;
	uint32_t const depth;
	uint32_t hiwat;

	AutoreleasePoolPageData(__unsafe_unretained id* _next, pthread_t _thread, AutoreleasePoolPage* _parent, uint32_t _depth, uint32_t _hiwat)
		: magic(), next(_next), thread(_thread),
		  parent(_parent), child(nil),
		  depth(_depth), hiwat(_hiwat)
	{
	}
};

```

`AutoreleasePoolPageData`作为`AutoreleasePoolPage`的父类定义了基本数据成员和结构。

- `magic`： 用于校验`AutoreleasePoolPage`是否完整。
- `next` ： 下一个push的autorelease对象保存的地址，初始为begin(),即`hiwat`下一个位置
- `thread` ： AutoreleasePoolPage 绑定的线程，一个自动释放池只能绑定一个线程
- `parent` ：`AutoreleasePoolPage`以链表的形式互相连接，`parent`指向上一个`AutoreleasePoolPage`
- `child`： `child`指向下一个`AutoreleasePoolPage`
- `depth`: 深度, 当前`AutoreleasePoolPage`在链表中的位置
- `hiwat`: high water mark

![](https://github.com/existorlive/existorlivepic/raw/master/AutoreleasePoolPage1.png)


`AutoreleasePoolPage`重写了`new`操作符，分配了一个内存页的内存(x86_64 为 4k); 除了`AutoreleasePoolPageData`中定义的基本数据成员，其他的内存空间作为连续内存的栈保存即将自动释放的对象地址，`next`指向栈顶。

```c++
// 重写了new运算符，申请4096字节的内存，即一个内存页
static void * operator new(size_t size) {
    return malloc_zone_memalign(malloc_default_zone(), SIZE, SIZE);
}
```

每个`AutoreleasePoolPage`占用一个内存页的大小，在当前`AutoreleasePoolPage`满了之后，会创建新的`AutoreleasePoolPage`，并通过链表的方式相互链接。

![](https://github.com/existorlive/existorlivepic/raw/master/AutoReleasePoolPage3.png)


**AutoreleasePool** 是可以嵌套的，但是并不是每嵌套一个**AutoreleasePool**都会创建一个`AutoreleasePoolPage`,而是通过分隔符`POOL_BOUNDARY`来分割

```c
#   define POOL_BOUNDARY nil
```

如下图，三个自动释放池嵌套在一起，通过分隔符`POOL_BOUNDARY`分割

![](https://github.com/existorlive/existorlivepic/raw/master/AutoReleasePoolPage4.png)


## 3. 示例

`_objc_autoreleasePoolPage()`方法可以打印当前自动释放池的具体信息。

有如下的示例，三个自动释放池嵌套，创建了800个自动释放的对象。

![](https://github.com/existorlive/existorlivepic/raw/master/%E6%88%AA%E5%B1%8F2021-07-22%20%E4%B8%8B%E5%8D%886.32.00.png)

自动释放池的具体信息输出如下：

![](https://github.com/existorlive/existorlivepic/raw/master/%E6%88%AA%E5%B1%8F2021-07-22%20%E4%B8%8B%E5%8D%886.33.04.png)

![](https://github.com/existorlive/existorlivepic/raw/master/%E6%88%AA%E5%B1%8F2021-07-22%20%E4%B8%8B%E5%8D%886.38.21.png)

可见共保存了803个对象，其中包括三个分隔符`POOL_BOUNDARY`对应三个自动释放池。

共分配了两个`AutoreleasePoolPage`，第一个`AutoreleasePoolPage`起始地址为`0x100810000`,栈底地址为`0x100810038`,栈顶地址为`0x100810ff8`

`0x100810ff8` - `0x100810000` + `8` = `4096`

正好为一个内存页大小



## 4.`AutoreleasePoolPage:push` 和 `AutoreleasePoolPage:autorelease` 

```c++
static inline void *push() 
{
    id *dest;
    if (slowpath(DebugPoolAllocation)) {
         // Each autorelease pool starts on a new pool page.
         dest = autoreleaseNewPage(POOL_BOUNDARY);
    } else {
        dest = autoreleaseFast(POOL_BOUNDARY);
    }
    ASSERT(dest == EMPTY_POOL_PLACEHOLDER || *dest == POOL_BOUNDARY);
    return dest;
}

    static inline id autorelease(id obj)
    {
        ASSERT(!obj->isTaggedPointerOrNil());
        id *dest __unused = autoreleaseFast(obj);
#if SUPPORT_AUTORELEASEPOOL_DEDUP_PTRS
        ASSERT(!dest  ||  dest == EMPTY_POOL_PLACEHOLDER  ||  (id)((AutoreleasePoolEntry *)dest)->ptr == obj);
#else
        ASSERT(!dest  ||  dest == EMPTY_POOL_PLACEHOLDER  ||  *dest == obj);
#endif
        return obj;
    }

```

## 5. `autoreleaseFast`



```c++
static inline id *autoreleaseFast(id obj)
{
    AutoreleasePoolPage *page = hotPage();
    if (page && !page->full()) {
        return page->add(obj);
    } else if (page) {
        return autoreleaseFullPage(obj, page);
    } else {
        return autoreleaseNoPage(obj);
    }
}
```
`autoreleaseFast`流程：
![](https://github.com/existorlive/existorlivepic/raw/master/AutoreleasePoolPage_push.png)


## 6. `AutoreleasePoolPage::pop`

```objc

    static inline void
    pop(void *token)
    {
        AutoreleasePoolPage *page;
        id *stop;
        if (token == (void*)EMPTY_POOL_PLACEHOLDER) {
            // Popping the top-level placeholder pool.
            page = hotPage();
            if (!page) {
                // Pool was never used. Clear the placeholder.
                return setHotPage(nil);
            }
            // Pool was used. Pop its contents normally.
            // Pool pages remain allocated for re-use as usual.
            page = coldPage();
            token = page->begin();
        } else {
            page = pageForPointer(token);
        }

        stop = (id *)token;
        if (*stop != POOL_BOUNDARY) {
            if (stop == page->begin()  &&  !page->parent) {
                // Start of coldest page may correctly not be POOL_BOUNDARY:
                // 1. top-level pool is popped, leaving the cold page in place
                // 2. an object is autoreleased with no pool
            } else {
                // Error. For bincompat purposes this is not 
                // fatal in executables built with old SDKs.
                return badPop(token);
            }
        }

        if (slowpath(PrintPoolHiwat || DebugPoolAllocation || DebugMissingPools)) {
            return popPageDebug(token, page, stop);
        }

        return popPage<false>(token, page, stop);
    }
```

```objc
    static void
    popPage(void *token, AutoreleasePoolPage *page, id *stop)
    {
        if (allowDebug && PrintPoolHiwat) printHiwat();

        page->releaseUntil(stop);

        // memory: delete empty children
        if (allowDebug && DebugPoolAllocation  &&  page->empty()) {
            // special case: delete everything during page-per-pool debugging
            AutoreleasePoolPage *parent = page->parent;
            page->kill();
            setHotPage(parent);
        } else if (allowDebug && DebugMissingPools  &&  page->empty()  &&  !page->parent) {
            // special case: delete everything for pop(top)
            // when debugging missing autorelease pools
            page->kill();
            setHotPage(nil);
        } else if (page->child) {
            // hysteresis: keep one empty child if page is more than half full
            if (page->lessThanHalfFull()) {
                page->child->kill();
            }
            else if (page->child->child) {
                page->child->child->kill();
            }
        }
    }
```

![](https://github.com/existorlive/existorlivepic/raw/master/AutoreleasePoolPage__pop.png)
