# 内存管理之弱引用

当把一个对象赋值给一个弱引用变量，该变量不会释放旧的对象，不会持有新的对象(不会调用`retain`，`release`管理引用计数的方法)。但是需要将弱引用变量记录下来，以便对象销毁后，将弱引用变量置为`nil`。

由上文我们知道：

- 在对象的`isa`中`weakly_referenced`占有一位，表示对象是否被弱引用
  
   ![](https://pic.existorlive.cn/isa64%E6%83%85%E5%86%B5.jpeg)

- 对象的弱引用变量保存在`SideTable`中的弱引用列表中`weak_table`

    
   ![](https://pic.existorlive.cn/SideTables.png)


```objc
int main(int argc, const char * argv[]) {
    @autoreleasepool {
        
        NSString * str = [NSString stringWithFormat:@"%@ %@",@"Hello",@"World"];
        __weak NSString * weakStr = str;
        
        NSLog(@"%@",weakStr);
        
        weakStr = nil;
        
    }
    return 0;
}
```

![](https://pic.existorlive.cn/%E6%88%AA%E5%B1%8F2021-07-22%20%E4%B8%8B%E5%8D%882.50.20.png)

通过断点调试，查看对应的汇编指令，可以看到一些与弱引用相关的符号

- `objc_initWeak`
- `objc_loadWeakRetained`
- `objc_storeweak`
- `objc_destoryWeak`

## 1. `objc_initWeak`

`objc_initWeak`初始化一个全新的弱引用, `__weak NSString * weakStr = str;`将会调用 `objc_initWeak`



```objc
id
objc_initWeak(id *location, id newObj)
{
    if (!newObj) {
        *location = nil;
        return nil;
    }

    return storeWeak<DontHaveOld, DoHaveNew, DoCrashIfDeallocating>
        (location, (objc_object*)newObj);
}
```

## 2. `objc_storeweak`

```objc
id
objc_storeWeak(id *location, id newObj)
{
    return storeWeak<DoHaveOld, DoHaveNew, DoCrashIfDeallocating>
        (location, (objc_object *)newObj);
}
```

## 3. `objc_destroyWeak`

```objc
void
objc_destroyWeak(id *location)
{
    (void)storeWeak<DoHaveOld, DontHaveNew, DontCrashIfDeallocating>
        (location, nil);
}
```


## `storeWeak`

- 查询oldObject和newObject的弱引用表
- 删除oldObject中的弱引用, 
- 增加newObject中的弱应用, 并设置isa中的WeaklyReferenced

```objc
static id 
storeWeak(id *location, objc_object *newObj)
{
    ASSERT(haveOld  ||  haveNew);
    if (!haveNew) ASSERT(newObj == nil);

    Class previouslyInitializedClass = nil;
    id oldObj;
    SideTable *oldTable;
    SideTable *newTable;

    // Acquire locks for old and new values.
    // Order by lock address to prevent lock ordering problems. 
    // Retry if the old value changes underneath us.
 retry:
    if (haveOld) {
        oldObj = *location;
        oldTable = &SideTables()[oldObj];
    } else {
        oldTable = nil;
    }
    if (haveNew) {
        newTable = &SideTables()[newObj];
    } else {
        newTable = nil;
    }

    SideTable::lockTwo<haveOld, haveNew>(oldTable, newTable);

    if (haveOld  &&  *location != oldObj) {
        SideTable::unlockTwo<haveOld, haveNew>(oldTable, newTable);
        goto retry;
    }

    // Prevent a deadlock between the weak reference machinery
    // and the +initialize machinery by ensuring that no 
    // weakly-referenced object has an un-+initialized isa.
    if (haveNew  &&  newObj) {
        Class cls = newObj->getIsa();
        if (cls != previouslyInitializedClass  &&  
            !((objc_class *)cls)->isInitialized()) 
        {
            SideTable::unlockTwo<haveOld, haveNew>(oldTable, newTable);
            class_initialize(cls, (id)newObj);

            // If this class is finished with +initialize then we're good.
            // If this class is still running +initialize on this thread 
            // (i.e. +initialize called storeWeak on an instance of itself)
            // then we may proceed but it will appear initializing and 
            // not yet initialized to the check above.
            // Instead set previouslyInitializedClass to recognize it on retry.
            previouslyInitializedClass = cls;

            goto retry;
        }
    }

    // Clean up old value, if any.
    if (haveOld) {
        weak_unregister_no_lock(&oldTable->weak_table, oldObj, location);
    }

    // Assign new value, if any.
    if (haveNew) {
        newObj = (objc_object *)
            weak_register_no_lock(&newTable->weak_table, (id)newObj, location, 
                                  crashIfDeallocating ? CrashIfDeallocating : ReturnNilIfDeallocating);
        // weak_register_no_lock returns nil if weak store should be rejected

        // Set is-weakly-referenced bit in refcount table.
        if (!newObj->isTaggedPointerOrNil()) {
            newObj->setWeaklyReferenced_nolock();
        }

        // Do not set *location anywhere else. That would introduce a race.
        *location = (id)newObj;
    }
    else {
        // No new value. The storage is not changed.
    }
    
    SideTable::unlockTwo<haveOld, haveNew>(oldTable, newTable);

    // This must be called without the locks held, as it can invoke
    // arbitrary code. In particular, even if _setWeaklyReferenced
    // is not implemented, resolveInstanceMethod: may be, and may
    // call back into the weak reference machinery.
    callSetWeaklyReferenced((id)newObj);

    return (id)newObj;
}
```

## 4. strong/weak/unsafe_unretained

```objc

void _object_setIvar(id obj, Ivar ivar, id value, bool assumeStrong)
{
    if (!ivar || obj->isTaggedPointerOrNil()) return;

    ptrdiff_t offset;
    objc_ivar_memory_management_t memoryManagement;
    _class_lookUpIvar(obj->ISA(), ivar, offset, memoryManagement);

    if (memoryManagement == objc_ivar_memoryUnknown) {
        if (assumeStrong) memoryManagement = objc_ivar_memoryStrong;
        else memoryManagement = objc_ivar_memoryUnretained;
    }

    id *location = (id *)((char *)obj + offset);

    switch (memoryManagement) {
    case objc_ivar_memoryWeak:       objc_storeWeak(location, value); break;
    case objc_ivar_memoryStrong:     objc_storeStrong(location, value); break;
    case objc_ivar_memoryUnretained: *location = value; break;
    case objc_ivar_memoryUnknown:    _objc_fatal("impossible");
    }
}
```