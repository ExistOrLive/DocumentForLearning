# atomic / copy / mutablecopy

## 1. atomic,strong

**atomic** 保证属性的访问是原子的

有如下代码：**Test**类的**str**属性是原子的

```objc
@interface Test : NSObject
@property(atomic,strong) NSString * str;
@end
@implementation Test
@end

int main(int argc, const char * argv[]) {
    @autoreleasepool {
        Test * test = [[Test alloc]init];
        test.str = @"dasda";
    }
    return 0;
}
```

添加符号断点`-[Test setStr:]`

![](https://github.com/existorlive/existorlivepic/raw/master/%E6%88%AA%E5%B1%8F2021-08-25%20%E4%B8%8B%E5%8D%8811.13.44.png)

`objc_setProperty_atomic`这个函数显然是和设置属性相关的函数，在**objc4.818.2**找到了对应的方法实现：

```c++

void objc_setProperty_atomic(id self, SEL _cmd, id newValue, ptrdiff_t offset)
{
    reallySetProperty(self, _cmd, newValue, offset, true, false, false);
}


static inline void reallySetProperty(id self, SEL _cmd, id newValue, ptrdiff_t offset, bool atomic, bool copy, bool mutableCopy)
{
    if (offset == 0) {
        object_setClass(self, newValue);
        return;
    }

    id oldValue;
    id *slot = (id*) ((char*)self + offset);

    if (copy) {
        newValue = [newValue copyWithZone:nil];
    } else if (mutableCopy) {
        newValue = [newValue mutableCopyWithZone:nil];
    } else {
        if (*slot == newValue) return;
        newValue = objc_retain(newValue);
    }

    if (!atomic) {
        oldValue = *slot;
        *slot = newValue;
    } else {
        spinlock_t& slotlock = PropertyLocks[slot];
        slotlock.lock();
        oldValue = *slot;
        *slot = newValue;        
        slotlock.unlock();
    }

    objc_release(oldValue);
}

```

最终调用到`reallySetProperty`这个方法，`atomic`参数为`true`,使用全局的自旋锁`spinlock_t` 保证同步。

但是这里注意`spinlock_t`锁的粒度很小，仅保证了赋值操作的原子性。


```swift
slotlock.lock();
oldValue = *slot; 
*slot = newValue;        
slotlock.unlock();
```

相关的内存管理方法并没有保证线程安全。如多线程对 `objc_release(oldValue)`对同一个`oldValue`多次调用导致问题。

因此`atomic`并不能保证线程安全。




## 2. atomic,copy

`Test`类的`str`改为 copy 修饰

```objc
@interface Test : NSObject
@property(atomic,copy) NSString * str;
@end
@implementation Test
@end

int main(int argc, const char * argv[]) {
    @autoreleasepool {
        Test * test = [[Test alloc]init];
        test.str = @"dasda";
    }
    return 0;
}
```

![](https://github.com/existorlive/existorlivepic/raw/master/%E6%88%AA%E5%B1%8F2021-08-25%20%E4%B8%8B%E5%8D%8811.33.39.png)

可见调用到`objc_setProperty_atomic_copy`,最终还是调用到`reallySetProperty`

```objc
void objc_setProperty_atomic_copy(id self, SEL _cmd, id newValue, ptrdiff_t offset)
{
    reallySetProperty(self, _cmd, newValue, offset, true, true, false);
}
```

此时`copy`参数为`true`，在设值之前会调用`copyWithZone:`拷贝一份。


## 3. nonatomic,strong

`Test`类的`str`改为 strong，nonatomic 修饰，同时也是默认的修饰属性

```objc
@interface Test : NSObject
@property(nonatomic,strong) NSString * str;
@end
@implementation Test
@end

int main(int argc, const char * argv[]) {
    @autoreleasepool {
        Test * test = [[Test alloc]init];
        test.str = @"dasda";
    }
    return 0;
}
```

![](https://github.com/existorlive/existorlivepic/raw/master/%E6%88%AA%E5%B1%8F2021-08-25%20%E4%B8%8B%E5%8D%8811.41.30.png)

`objc_storeStrong` 仅作持有新值，释放旧值的操作

```c
void
objc_storeStrong(id *location, id obj)
{
    id prev = *location;
    if (obj == prev) {
        return;
    }
    objc_retain(obj);
    *location = obj;
    objc_release(prev);
}

```

## 4. nonatomic,weak / atomic,weak

```objc
@interface Test : NSObject
@property(nonatomic,weak) NSString * str;
@end
@implementation Test
@end

int main(int argc, const char * argv[]) {
    @autoreleasepool {
        Test * test = [[Test alloc]init];
        test.str = @"dasda";
    }
    return 0;
}
```

![](https://github.com/existorlive/existorlivepic/raw/master/%E6%88%AA%E5%B1%8F2021-08-25%20%E4%B8%8B%E5%8D%8811.42.13.png)

不论 nonatomic,weak 还是 atomic,weak ，都是调用到 `objc_storeWeak`

弱引用保存在全局的`SideTable`，对`SideTable`的操作必须是在临界区中，一定是线程安全的，所以对于weak来说， atomic/nonatomic 是一样的

```c++
id
objc_storeWeak(id *location, id newObj)
{
    return storeWeak<DoHaveOld, DoHaveNew, DoCrashIfDeallocating>
        (location, (objc_object *)newObj);
}

static id 
storeWeak(id *location, objc_object *newObj)
{
    ASSERT(haveOld  ||  haveNew);
    if (!haveNew) ASSERT(newObj == nil);

    Class previouslyInitializedClass = nil;
    id oldObj;
    SideTable *oldTable;
    SideTable *newTable;

    // Acquire locks for old and new values.
    // Order by lock address to prevent lock ordering problems. 
    // Retry if the old value changes underneath us.
 retry:
    if (haveOld) {
        oldObj = *location;
        oldTable = &SideTables()[oldObj];
    } else {
        oldTable = nil;
    }
    if (haveNew) {
        newTable = &SideTables()[newObj];
    } else {
        newTable = nil;
    }

    SideTable::lockTwo<haveOld, haveNew>(oldTable, newTable);

    if (haveOld  &&  *location != oldObj) {
        SideTable::unlockTwo<haveOld, haveNew>(oldTable, newTable);
        goto retry;
    }

    // Prevent a deadlock between the weak reference machinery
    // and the +initialize machinery by ensuring that no 
    // weakly-referenced object has an un-+initialized isa.
    if (haveNew  &&  newObj) {
        Class cls = newObj->getIsa();
        if (cls != previouslyInitializedClass  &&  
            !((objc_class *)cls)->isInitialized()) 
        {
            SideTable::unlockTwo<haveOld, haveNew>(oldTable, newTable);
            class_initialize(cls, (id)newObj);

            // If this class is finished with +initialize then we're good.
            // If this class is still running +initialize on this thread 
            // (i.e. +initialize called storeWeak on an instance of itself)
            // then we may proceed but it will appear initializing and 
            // not yet initialized to the check above.
            // Instead set previouslyInitializedClass to recognize it on retry.
            previouslyInitializedClass = cls;

            goto retry;
        }
    }

    // Clean up old value, if any.
    if (haveOld) {
        weak_unregister_no_lock(&oldTable->weak_table, oldObj, location);
    }

    // Assign new value, if any.
    if (haveNew) {
        newObj = (objc_object *)
            weak_register_no_lock(&newTable->weak_table, (id)newObj, location, 
                                  crashIfDeallocating ? CrashIfDeallocating : ReturnNilIfDeallocating);
        // weak_register_no_lock returns nil if weak store should be rejected

        // Set is-weakly-referenced bit in refcount table.
        if (!newObj->isTaggedPointerOrNil()) {
            newObj->setWeaklyReferenced_nolock();
        }

        // Do not set *location anywhere else. That would introduce a race.
        *location = (id)newObj;
    }
    else {
        // No new value. The storage is not changed.
    }
    
    SideTable::unlockTwo<haveOld, haveNew>(oldTable, newTable);

    // This must be called without the locks held, as it can invoke
    // arbitrary code. In particular, even if _setWeaklyReferenced
    // is not implemented, resolveInstanceMethod: may be, and may
    // call back into the weak reference machinery.
    callSetWeaklyReferenced((id)newObj);

    return (id)newObj;
}

```
## 5. nonatomic,unsafe_unretained / nonatomic,unsafe_unretained

```objc
@interface Test : NSObject
@property(nonatomic,unsafe_unretained) NSString * str;
@end
@implementation Test
@end

int main(int argc, const char * argv[]) {
    @autoreleasepool {
        Test * test = [[Test alloc]init];
        test.str = @"dasda";
    }
    return 0;
}
```

![](https://github.com/existorlive/existorlivepic/raw/master/%E6%88%AA%E5%B1%8F2021-08-25%20%E4%B8%8B%E5%8D%8811.43.10.png)

在这里，`setStr:` 就是单纯的赋值操作，不涉及任何内存管理和线程安全的代码





## 6. block 为什么用 copy 修饰

block的retain操作其实调用的是 `_Block_copy`函数：

当 block 在栈上时，会拷贝到堆上；


## 7. NSString 为什么用 copy 修饰

当赋值对象为**NSString**， `copy` 和 `strong` 其实是一样的，啥也不做


当赋值对象为**NSMutableString**， `copy`为拷贝一份**NSString**,以保证属性不会改变



