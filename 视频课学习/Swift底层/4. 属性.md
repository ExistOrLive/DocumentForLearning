# 属性

属性的基本使用请参考[属性](../../开发语言学习/Swift/9.%20类和结构体/2.%20属性)

## 1. 存储属性和计算属性

如下定义`class ZMTest`, 其中 `param1` 为存储属性，`param2` 为计算属性
```swift
class ZMTest{
    var param1: Int = 11
    var param2: Int{
        get{
            return 11
        }
    }
}
```

`swiftc -emit-sil main.swift | xcrun swift-demangle >> ./main.sil`生成对应的SIL文件

```swift
class ZMTest {
  @_hasStorage @_hasInitialValue var param1: Int { get set }
  var param2: Int { get }
  @objc deinit
  init()
}
```

`param1`有`@_hasStorage`修饰表明其为存储属性,需要为分配属性所需的内存 ;`param2`没有，则为计算属性，不再用实例的内存

![](https://gitee.com/existorlive/exist-or-live-pic/raw/master/%E6%88%AA%E5%B1%8F2021-08-04%20%E4%B8%8A%E5%8D%884.58.47.png)


## 2. 属性观察者

```swift
class ZMTest{
    var param1: Int = 11{
        didSet{
            print(oldValue)
        }
        willSet{
            print(newValue)
        }
    }
    var param2: Int{
        get{
            return 11
        }
    }
}
```

得到对应的SIL文件，其中 `ZMTest.param1.setter`的实现如下：

`ZMTest.param1.willset`和`ZMTest.param1.didset`定义为两个全局函数，分别在`param1`赋值前和赋值后调用

```swift
// ZMTest.param1.setter
sil hidden @main.ZMTest.param1.setter : Swift.Int : $@convention(method) (Int, @guaranteed ZMTest) -> () {
// %0 "value"                                     // users: %13, %10, %2
// %1 "self"                                      // users: %16, %11, %10, %4, %3
bb0(%0 : $Int, %1 : $ZMTest):
  debug_value %0 : $Int, let, name "value", argno 1 // id: %2
  debug_value %1 : $ZMTest, let, name "self", argno 2 // id: %3
  %4 = ref_element_addr %1 : $ZMTest, #ZMTest.param1 // user: %5
  %5 = begin_access [read] [dynamic] %4 : $*Int   // users: %6, %7
  %6 = load %5 : $*Int                            // users: %8, %16
  end_access %5 : $*Int                           // id: %7
  debug_value %6 : $Int, let, name "tmp"          // id: %8
  // 调用 function_ref ZMTest.param1.willset 
  %9 = function_ref @main.ZMTest.param1.willset : Swift.Int : $@convention(method) (Int, @guaranteed ZMTest) -> () // user: %10
  %10 = apply %9(%0, %1) : $@convention(method) (Int, @guaranteed ZMTest) -> ()
  %11 = ref_element_addr %1 : $ZMTest, #ZMTest.param1 // user: %12
  %12 = begin_access [modify] [dynamic] %11 : $*Int // users: %13, %14
  // 赋值 param1
  store %0 to %12 : $*Int                         // id: %13
  end_access %12 : $*Int                          // id: %14
  // 调用 function_ref ZMTest.param1.didset
  %15 = function_ref @main.ZMTest.param1.didset : Swift.Int : $@convention(method) (Int, @guaranteed ZMTest) -> () // user: %16
  %16 = apply %15(%6, %1) : $@convention(method) (Int, @guaranteed ZMTest) -> ()
  %17 = tuple ()                                  // user: %18
  return %17 : $()                                // id: %18
} // end sil function 'main.ZMTest.param1.setter : Swift.Int'
```

## 3. lazy属性 (延迟存储属性)

- 延迟存储属性的初始化时机延迟到第一次使用
- 延迟存储属性并不能保证线程安全

延迟存储属性具体使用请参考[属性](../../开发语言学习/Swift/9.%20类和结构体/2.%20属性/2.1%20属性.md)


```swift
class ZMTest{
    lazy var param1: Int = {var res = 0; for i in 0...100{ res = res + 1 }; return res}()
}

let test = ZMTest()
print(test.param1)
```
得到对应的SIL，lazy属性`param1`实际上是一个计算属性，同时生成一个对应的存储属性`$__lazy_storage_$_param1`,且类型为可选类型。

```swift 
class ZMTest {
  lazy var param1: Int { get set }
  @_hasStorage @_hasInitialValue final var $__lazy_storage_$_param1: Int? { get set }
  @objc deinit
  init()
}
```

`ZMTest.param1.getter`的实现如下：

1. 读取存储属性`$__lazy_storage_$_param1`, 
2. 如果是不为空，则解包为非可选类型并返回；
3. 如果为空，则执行初始化的函数或者闭包的执行，并将结果保存至`$__lazy_storage_$_param1`中。最终返回结果

我们可以注意到`ZMTest.param1.getter` 并没有任何的同步或者互斥的代码，因此lazy属性并不是线程安全。

```swift
// ZMTest.param1.getter
sil hidden [lazy_getter] [noinline] @main.ZMTest.param1.getter : Swift.Int : $@convention(method) (@guaranteed ZMTest) -> Int {
// %0 "self"                                      // users: %14, %2, %1
bb0(%0 : $ZMTest):
  debug_value %0 : $ZMTest, let, name "self", argno 1 // id: %1
  %2 = ref_element_addr %0 : $ZMTest, #ZMTest.$__lazy_storage_$_param1 // user: %3
  %3 = begin_access [read] [dynamic] %2 : $*Optional<Int> // users: %4, %5
  %4 = load %3 : $*Optional<Int>                  // user: %6
  end_access %3 : $*Optional<Int>                 // id: %5
  switch_enum %4 : $Optional<Int>, case #Optional.some!enumelt: bb1, case #Optional.none!enumelt: bb2 // id: %6

// %7                                             // users: %9, %8
bb1(%7 : $Int):                                   // Preds: bb0
  debug_value %7 : $Int, let, name "tmp1"         // id: %8
  br bb3(%7 : $Int)                               // id: %9

bb2:                                              // Preds: bb0
  // function_ref closure #1 in ZMTest.param1.getter
  %10 = function_ref @closure #1 () -> Swift.Int in main.ZMTest.param1.getter : Swift.Int : $@convention(thin) () -> Int // user: %11
  %11 = apply %10() : $@convention(thin) () -> Int // users: %18, %13, %12
  debug_value %11 : $Int, let, name "tmp2"        // id: %12
  %13 = enum $Optional<Int>, #Optional.some!enumelt, %11 : $Int // user: %16
  %14 = ref_element_addr %0 : $ZMTest, #ZMTest.$__lazy_storage_$_param1 // user: %15
  %15 = begin_access [modify] [dynamic] %14 : $*Optional<Int> // users: %16, %17
  store %13 to %15 : $*Optional<Int>              // id: %16
  end_access %15 : $*Optional<Int>                // id: %17
  br bb3(%11 : $Int)                              // id: %18

// %19                                            // user: %20
bb3(%19 : $Int):                                  // Preds: bb2 bb1
  return %19 : $Int                               // id: %20
} // end sil function 'main.ZMTest.param1.getter : Swift.Int'
```

## 4. 类属性

`实例属性`属于一个特定类型的实例；`类属性`属于整个类型，在多个该类型的实例间共享。


`类属性`既可以定义为`存储属性`也可以定义为`计算属性`

`存储的类属性`必须在定义时提供初始化器。但是`类型属性`在首次访问时才初始化，同时`类型属性`的初始化是线程安全的。

类属性具体使用请参考[类属性](../../开发语言学习/Swift/9.%20类和结构体/2.%20属性/2.5%20类属性.md)

```swift
class ZMTest{
    static var classParam1 : Int = 11
    static var classParam2 : Int {
        get{
            return 12
        }
    }
}
```

SIL 如下：

```
class ZMTest {
  @_hasStorage @_hasInitialValue static var classParam1: Int { get set }
  class var classParam2: Int { get }
  @objc deinit
  init()
}
```

`static ZMTest.classParam1`的 `setter` 和 `getter`的实现如下：

可以看到在访问`static ZMTest.classParam1` 都会调用 `@main.ZMTest.classParam1.unsafeMutableAddressor`这个函数：

```swift
// static ZMTest.classParam1.getter
sil hidden [transparent] @static main.ZMTest.classParam1.getter : Swift.Int : $@convention(method) (@thick ZMTest.Type) -> Int {
// %0 "self"                                      // user: %1
bb0(%0 : $@thick ZMTest.Type):
  debug_value %0 : $@thick ZMTest.Type, let, name "self", argno 1 // id: %1
  // function_ref ZMTest.classParam1.unsafeMutableAddressor
  %2 = function_ref @main.ZMTest.classParam1.unsafeMutableAddressor : Swift.Int : $@convention(thin) () -> Builtin.RawPointer // user: %3
  %3 = apply %2() : $@convention(thin) () -> Builtin.RawPointer // user: %4
  %4 = pointer_to_address %3 : $Builtin.RawPointer to [strict] $*Int // user: %5
  %5 = begin_access [read] [dynamic] %4 : $*Int   // users: %6, %7
  %6 = load %5 : $*Int                            // user: %8
  end_access %5 : $*Int                           // id: %7
  return %6 : $Int                                // id: %8
} // end sil function 'static main.ZMTest.classParam1.getter : Swift.Int'

// static ZMTest.classParam1.setter
sil hidden [transparent] @static main.ZMTest.classParam1.setter : Swift.Int : $@convention(method) (Int, @thick ZMTest.Type) -> () {
// %0 "value"                                     // users: %8, %2
// %1 "self"                                      // user: %3
bb0(%0 : $Int, %1 : $@thick ZMTest.Type):
  debug_value %0 : $Int, let, name "value", argno 1 // id: %2
  debug_value %1 : $@thick ZMTest.Type, let, name "self", argno 2 // id: %3
  // function_ref ZMTest.classParam1.unsafeMutableAddressor
  %4 = function_ref @main.ZMTest.classParam1.unsafeMutableAddressor : Swift.Int : $@convention(thin) () -> Builtin.RawPointer // user: %5
  %5 = apply %4() : $@convention(thin) () -> Builtin.RawPointer // user: %6
  %6 = pointer_to_address %5 : $Builtin.RawPointer to [strict] $*Int // user: %7
  %7 = begin_access [modify] [dynamic] %6 : $*Int // users: %8, %9
  store %0 to %7 : $*Int                          // id: %8
  end_access %7 : $*Int                           // id: %9
  %10 = tuple ()                                  // user: %11
  return %10 : $()                                // id: %11
} // end sil function 'static main.ZMTest.classParam1.setter : Swift.Int'
```


`@main.ZMTest.classParam1.unsafeMutableAddressor`实现如下: 利用once实现了线程安全且全局执行一次初始化`ZMTest.classParam1`

```swift
// ZMTest.classParam1.unsafeMutableAddressor
sil hidden [global_init] @main.ZMTest.classParam1.unsafeMutableAddressor : Swift.Int : $@convention(thin) () -> Builtin.RawPointer {
bb0:
  %0 = global_addr @one-time initialization token for classParam1 : $*Builtin.Word // user: %1
  %1 = address_to_pointer %0 : $*Builtin.Word to $Builtin.RawPointer // user: %3
  // function_ref one-time initialization function for classParam1
  %2 = function_ref @one-time initialization function for classParam1 : $@convention(c) () -> () // user: %3
  %3 = builtin "once"(%1 : $Builtin.RawPointer, %2 : $@convention(c) () -> ()) : $()
  %4 = global_addr @static main.ZMTest.classParam1 : Swift.Int : $*Int // user: %5
  %5 = address_to_pointer %4 : $*Int to $Builtin.RawPointer // user: %6
  return %5 : $Builtin.RawPointer                 // id: %6
} // end sil function 'main.ZMTest.classParam1.unsafeMutableAddressor : Swift.Int'
```